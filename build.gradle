apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: "org.sonarqube"

dependencies {
    // ALL
    implementation(project(':commons-android')) {
        exclude group: 'com.google.protobuf', module: 'protobuf-java'
    }
    implementation rootProject.ext.supportLibs.appCompat
    implementation rootProject.ext.supportLibs.customTabs
    implementation rootProject.ext.supportLibs.design
    implementation rootProject.ext.supportLibs.supportV4
    implementation rootProject.ext.gpsLibs.analytics
    implementation rootProject.ext.gpsLibs.basement // from commons-android
    implementation rootProject.ext.gpsLibs.location
    implementation rootProject.ext.gpsLibs.maps
    implementation rootProject.ext.gpsLibs.firebase_ads
    implementation rootProject.ext.gpsLibs.firebase_analytics
    implementation rootProject.ext.gpsLibs.firebase_core
    implementation(rootProject.ext.gpsLibs.firebase_crashlytics_aar) {
        transitive = true
    }
    implementation rootProject.ext.fbLibs.audienceNetwork
    implementation rootProject.ext.fbLibs.audienceNetworkMediation
    // DEBUG
    debugImplementation rootProject.ext.squareLibs.leakcanary
    debugImplementation rootProject.ext.squareLibs.leakcanary_support_fragment
    // RELEASE
    releaseImplementation rootProject.ext.squareLibs.leakcanary_no_op
    // TEST
    testImplementation rootProject.ext.squareLibs.leakcanary_no_op
    // DEBUG
    debugImplementation 'androidx.multidex:multidex:2.0.1'

    testImplementation rootProject.ext.jUnitLibs.jUnit
    testImplementation rootProject.ext.mockitoLibs.mockitoCore
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    Properties projectNameProperties = new Properties()
    projectNameProperties.load(new FileInputStream(file("project_name.properties")))

    def projectName = projectNameProperties["project.name"]

    Properties versionProperties = new Properties()
    versionProperties.load(new FileInputStream(file("version.properties")))

    Properties keysProperties = new Properties()
    def keysPropertiesFile = file("keys.properties")
    if (keysPropertiesFile.exists()) {
        keysProperties.load(new FileInputStream(keysPropertiesFile))
    } else {
        keysPropertiesFile = file("../keys.properties")
        if (keysPropertiesFile.exists()) {
            keysProperties.load(new FileInputStream(keysPropertiesFile))
        }
    }

    def resValueFr = file("res/values-fr")

    defaultConfig {
        applicationId "org.mtransit.android"
        versionCode versionProperties['version.code'] as Integer
        versionName versionProperties['version.name']
        setProperty("archivesBaseName", "${projectName}_v${versionName}_r${versionCode}")
        if (resValueFr.exists()) {
            resConfigs "en", "fr"
        } else {
            resConfigs "en"
        }
        minSdkVersion rootProject.ext.minSdkVersion
        // minSdkVersion 21 // DEBUG avoid using legacy multidex
        targetSdkVersion rootProject.ext.targetSdkVersion
    }

    signingConfigs {
        release {
            if (keysPropertiesFile.exists()) {
                storeFile file(keysProperties['key.store'])
                storePassword keysProperties['key.store.password']
                keyAlias keysProperties['key.alias']
                keyPassword keysProperties['key.alias.password']
            }
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "_DEBUG"

            // testCoverageEnabled false // DEBUG skip coverage report
            shrinkResources false
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
            multiDexEnabled true
            // Disable fabric build ID generation for debug builds
            ext.enableCrashlytics = false
            // https://docs.fabric.io/android/crashlytics/build-tools.html
            ext.alwaysUpdatedBuildId = false
            aaptOptions.cruncherEnabled = false
        }
        //noinspection GroovyMissingReturnStatement
        release {
            if (keysPropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }

            shrinkResources true
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
        }
    }

    //noinspection GroovyMissingReturnStatement
    lintOptions {
        abortOnError true
        warningsAsErrors false
        ignoreWarnings false
        quiet false
        checkDependencies true // also check leaf modules
        // checkAllWarnings true // too slow for CI, can be enabled locally
        ignoreTestSources true
        warning 'InvalidPackage', //
                'ClickableViewAccessibility', //
                'MergeRootFrame', //
                'Overdraw', //
                'UselessParent', //
                'IconDensities',  //
                'RtlHardcoded'
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
        test {
            java.srcDirs = ['srcTest']
            resources.srcDirs = ['srcTest']
        }
        androidTest {
            java.srcDirs = ['srcAndroidTest']
            resources.srcDirs = ['srcAndroidTest']
        }

        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
            all {
                jvmArgs '-noverify'
                testLogging {
                    events 'passed', 'skipped', 'failed'
                }
            }
        }
    }

    def publish = project.tasks.create("copyReleaseApkToOutputDirs")
    publish.description "Copies release APK to output directory"
    android.applicationVariants.all { variant ->
        if (variant.buildType.name == "release") {
            Properties customLocalProperties = new Properties()
            def customLocalPropertiesFile = file("../keys.properties")
            if (customLocalPropertiesFile.exists()) {
                customLocalProperties.load(new FileInputStream(file("../custom_local.properties")))
                def outputDir = customLocalProperties['output.dir']
                def outputCloudDir = customLocalProperties['output.cloud.dir']
                variant.outputs.each { output ->
                    if (output.outputFile != null && output.outputFile.name.endsWith('.apk')) {
                        def copyToOutputDirTask = project.tasks.create("copyToOutputDir${variant.name}Apk", Copy)
                        def copyToOutputCloudDirTask = project.tasks.create("copyToOutputCloudDir${variant.name}Apk", Copy)
                        copyToOutputDirTask.from(output.outputFile)
                        copyToOutputCloudDirTask.from(output.outputFile)
                        copyToOutputDirTask.into(outputDir)
                        copyToOutputCloudDirTask.into(outputCloudDir)
                        copyToOutputDirTask.dependsOn variant.assemble
                        copyToOutputCloudDirTask.dependsOn variant.assemble
                        publish.dependsOn copyToOutputDirTask
                        publish.dependsOn copyToOutputCloudDirTask
                    }
                }
            }
        }
    }
}

apply plugin: 'com.google.gms.google-services'
